subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    java.toolchain.languageVersion = JavaLanguageVersion.of(8)
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"
    javadoc.options.encoding = "UTF-8"

    archivesBaseName = "MCStructs-" + project.maven_name
    group = project.maven_group
    version = project.maven_version

    repositories {
        mavenCentral()
        maven {
            name = "lenni0451 releases"
            url = "https://maven.lenni0451.net/releases"
        }
    }

    dependencies {
        implementation "com.google.code.findbugs:jsr305:3.0.2"

        testImplementation platform("org.junit:junit-bom:5.9.0")
        testImplementation "org.junit.jupiter:junit-jupiter"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        maxParallelForks Runtime.runtime.availableProcessors()
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    publishing {
        repositories {
//            maven {
//                name = "reposilite"
//                url = ((project.snapshot == "true") ? "https://maven.lenni0451.net/snapshots" : "https://maven.lenni0451.net/releases")
//                credentials(PasswordCredentials)
//                authentication {
//                    basic(BasicAuthentication)
//                }
//            }
            maven {
                name = "ossrh"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
//                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                credentials(PasswordCredentials)
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        publications {
            maven(MavenPublication) {
                artifactId = project.maven_name
                groupId = project.maven_group
                version = project.maven_version

                from components.java

                pom {
                    name = "MCStructs"
                    description = "A minecraft data structure library with the goal of being compliant with the vanilla client"
                    url = "https://github.com/Lenni0451/MCStructs"
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/Lenni0451/MCStructs/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "Lenni0451"
                        }
                    }
                    scm {
                        url = "https://github.com/Lenni0451/MCStructs.git"
                    }
                }
            }
        }
    }

    signing {
        sign configurations.archives
        sign publishing.publications.maven
    }

    project.tasks.withType(PublishToMavenRepository).forEach {
        it.dependsOn(project.tasks.withType(Sign))
    }
    build.dependsOn(test)
}
